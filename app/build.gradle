apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def readAndUnderstoodLicense = false

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "io.github.domi04151309.home"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 180
        versionName "1.8.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix ' debug'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable "MissingTranslation"
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

preBuild.doFirst {
    def data = file('./src/main/res/xml/pref_about.xml').getText().contains('app:key="license"')
    if (!data) throw new GradleException(
            'Please note that removing the license from the about page is not allowed if you plan' +
                    'to publish your modified version of this app. ' +
                    'Please read the project\'s LICENSE.'
    )
    if (!(android.defaultConfig.applicationId.contains('domi04151309') || readAndUnderstoodLicense)) {
        throw new GradleException(
                'Please make sure you have read and understood the LICENSE!'
        )
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha03'
    implementation "com.github.skydoves:colorpickerview:2.2.3"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7'
}
